import java.time.LocalDateTime

boolean useWipVersion = hasProperty('wipVersion')

if (!useWipVersion) {
    var out = new StringBuilder()
    'git rev-parse --abbrev-ref HEAD'.execute().waitForProcessOutput(out, null)
    useWipVersion = out.contains('develop') || out.contains("feature")
}

LocalDateTime now = LocalDateTime.now()
String wipVer = now.format('yyyy.MM.dd.HH') + '.' + String.format("%02d", now.getMinute().intdiv(30) * 30) + ".wip"

version = useWipVersion ? wipVer : '0.4.6.1'

println(version)

subprojects {
    apply plugin: 'java'

    group = 'com.github.mrmks.mc'
    version = rootProject.version // major.protocol.minor.fix

    java {
        archivesBaseName = 'EffekseerCraft'
        toolchain.languageVersion = JavaLanguageVersion.of(8)
    }

    configurations {
        shade
        implementation.extendsFrom shade
    }
}
